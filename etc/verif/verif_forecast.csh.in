#!@CSH@ 

# First argument is $LAPS_DATA_ROOT

# Second argument is number of forecast time steps (including analysis)

# Third argument is the type "pt" or "grid"

# Fourth argument is the variable (upper case)

# Fifth argument is the yrange

# Sixth argument is the overall title variable

# Seventh argument is the units

setenv LAPSINSTALLROOT @prefix@
setenv LAPS_DATA_ROOT $1
setenv NLINE $2
setenv TYPE $3
setenv VAR $4
setenv YRANGE $5

setenv VAR_LC `echo $VAR | gawk '{print tolower($0)}'`

#setenv LAPS_DATA_ROOT /data/fab/projects/dwr
setenv LOGDIR $LAPS_DATA_ROOT/log

setenv MODELTIME `head -1 $LAPS_DATA_ROOT/time/modeltime.dat`
setenv MODELTIME_HHMM `head -1 $LAPS_DATA_ROOT/time/modeltime.dat | cut -c6-9`

setenv MODEL_ASCIITIME `tail -1 $LAPS_DATA_ROOT/time/modeltime.dat`

setenv FILE $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/$MODELTIME.stats
setenv MEMBERS_FILE $LAPS_DATA_ROOT/lapsprd/verif/members.txt

echo " "
echo "inputs..."
echo "TYPE = $TYPE"  
echo "VAR = $VAR"  
echo "FILE = $FILE"  
ls -l $FILE                                                     

#@ NLINE = $NLINE + 1

# obtain xrange times from the data file
setenv STARTTIME1 `head -1       $FILE           | cut -c2-12` ; echo $STARTTIME1
setenv STARTTIME2 `head -1       $FILE           | cut -c14-24`; echo $STARTTIME2
setenv STOPTIME1  `head -$NLINE $FILE | tail -1 | cut -c1-12` ; echo $STOPTIME1
setenv STOPTIME2  `head -$NLINE $FILE | tail -1 | cut -c14-24`; echo $STOPTIME2

echo " "
echo "Start time: $STARTTIME1 $STARTTIME2"
echo "Stop  time: $STOPTIME1 $STOPTIME2 NLINE is $NLINE"

setenv NMEMBERS `wc -l $MEMBERS_FILE | cut -c1-2`
echo "NMEMBERS: $NMEMBERS"

# Construct gnuplot .gp file
setenv GNUPLOTFILE $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/$VAR_LC\_$TYPE\_fcst.gp
setenv PNGDIR      $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/
setenv PNGFILE     $VAR_LC\_$TYPE\_fcst\_$MODELTIME.png
setenv DQUOTE \"
echo 'set terminal png size 1200,800'                                     > $GNUPLOTFILE
echo 'set xdata time'                                                    >> $GNUPLOTFILE
echo 'set timefmt "%d-%b-%Y %H:%M:%S"'                                   >> $GNUPLOTFILE
echo 'set output '$DQUOTE$PNGDIR$PNGFILE$DQUOTE                          >> $GNUPLOTFILE

# time range must be in same format as data file
#echo 'set xrange ["2-FEB-2011 12:00:00.00":"2-FEB-2011 18:00:00.00"]'    >> $GNUPLOTFILE
echo 'set xrange ['$DQUOTE$STARTTIME1' '$STARTTIME2$DQUOTE':'$DQUOTE$STOPTIME1' '$STOPTIME2$DQUOTE']' >> $GNUPLOTFILE
#echo 'set yrange [0:'$YRANGE']'                                           >> $GNUPLOTFILE
echo 'set yrange ['$YRANGE']'                                             >> $GNUPLOTFILE
echo 'set grid'                                                           >> $GNUPLOTFILE
echo 'set datafile missing "-99.900"'                                     >> $GNUPLOTFILE
echo 'set xlabel "Initialized '$MODEL_ASCIITIME' UTC"'                    >> $GNUPLOTFILE 
echo 'set ylabel "'$6 \($7\)' "'                                          >> $GNUPLOTFILE
echo 'set title "'$6' Observed vs Forecast"'                              >> $GNUPLOTFILE
echo 'set key left box'                                                   >> $GNUPLOTFILE

if ($VAR == "WSF" || $VAR == "W3") then
    setenv T1 "RMSE U"    
    setenv T2 "RMSE V"    
    setenv T3 "RMSE Vector"    

    echo 'set key width 1'                                                                           >> $GNUPLOTFILE
else
    setenv T1 "Fcst Mean"
    setenv T2 "Obs Mean"
    setenv T3 "RMS (F-O)"

    echo 'set key width 1'                                                                           >> $GNUPLOTFILE
endif

setenv IMEMBER 1
while ($IMEMBER <= $NMEMBERS)
    @ IBLOCK = $IMEMBER - 1

    setenv MEMBER `head -$IMEMBER $MEMBERS_FILE | tail -1`
    echo "IMEMBER MEMBER IBLOCK: $IMEMBER $MEMBER $IBLOCK"

    if ($NMEMBERS == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:3 title "'$T1'" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:4 title "'$T2'" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:5 title "'$T3'" with linespoints lw 3 pt 5 ps 1'    >> $GNUPLOTFILE
    else if ($IMEMBER == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:3 title "'$T1'" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:4 title "'$T2'" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:5 title "'$T3'" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
    else if ($IMEMBER == $NMEMBERS) then
        echo '     '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:3               with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:4               with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:5               with linespoints lw 3 pt 5 ps 1'    >> $GNUPLOTFILE
    else
        echo '     '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:3               with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:4               with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using 1:5               with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
    endif

    @ IMEMBER = $IMEMBER + 1
end

# Run gnuplot
gnuplot < $GNUPLOTFILE

echo " "
echo "outputs..."
ls -l $GNUPLOTFILE
cd $PNGDIR
ls -l $PNGFILE                          

rm -f          $VAR_LC\_$TYPE\_fcst.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst.png

rm -f          $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png

convert -resize 90x60! $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM\_thumb.png

