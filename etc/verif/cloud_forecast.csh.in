#!@CSH@

# First argument is $LAPS_DATA_ROOT

# Second argument is number of forecast time steps

setenv LAPSINSTALLROOT @prefix@
setenv LAPS_DATA_ROOT $1
setenv NLINE $2

#setenv LAPS_DATA_ROOT /data/fab/projects/dwr
setenv LOGDIR $LAPS_DATA_ROOT/log

setenv MODELTIME      `head -1 $LAPS_DATA_ROOT/time/modeltime.dat`
setenv MODELTIME_HHMM `head -1 $LAPS_DATA_ROOT/time/modeltime.dat | cut -c6-9`

setenv MODEL_ASCIITIME `tail -1 $LAPS_DATA_ROOT/time/modeltime.dat`


setenv FILE $LAPS_DATA_ROOT/lapsprd/verif/SWI/pt/$MODELTIME.stats

echo " "
echo "inputs..."
echo "FILE = $FILE"
ls -l $FILE

@ NLINE = $NLINE + 1

# obtain xrange times from the data file
setenv STARTTIME1 `head -1       $FILE           | cut -c2-12` ; echo $STARTTIME1
setenv STARTTIME2 `head -1       $FILE           | cut -c14-24`; echo $STARTTIME2
setenv STOPTIME1  `head -$NLINE $FILE | tail -1 | cut -c1-12` ; echo $STOPTIME1
setenv STOPTIME2  `head -$NLINE $FILE | tail -1 | cut -c14-24`; echo $STOPTIME2

# Construct gnuplot .gp file
#setenv GNUPLOTFILE $LOGDIR/cloud_fcst.gp

setenv VAR SWI
setenv VAR_LC swi
setenv TYPE pt
setenv GNUPLOTFILE $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/$VAR_LC\_$TYPE\_fcst.gp
setenv PNGDIR      $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/
mkdir -p $PNGDIR
setenv PNGFILE     $VAR_LC\_$TYPE\_fcst\_$MODELTIME.png

setenv DQUOTE \"
echo 'set terminal png size 1200,800'                                     > $GNUPLOTFILE
echo 'set xdata time'                                                    >> $GNUPLOTFILE
echo 'set timefmt "%d-%b-%Y %H:%M:%S"'                                   >> $GNUPLOTFILE
#echo 'set output '$DQUOTE$LOGDIR'/cloud_fcst.png'$DQUOTE                 >> $GNUPLOTFILE
echo 'set output '$DQUOTE$PNGDIR$PNGFILE$DQUOTE                          >> $GNUPLOTFILE

# time range must be in same format as data file
#echo 'set xrange ["2-FEB-2011 12:00:00.00":"2-FEB-2011 18:00:00.00"]'    >> $GNUPLOTFILE
echo 'set xrange ['$DQUOTE$STARTTIME1' '$STARTTIME2$DQUOTE':'$DQUOTE$STOPTIME1' '$STOPTIME2$DQUOTE']' >> $GNUPLOTFILE
echo 'set yrange [0:1000]'                                               >> $GNUPLOTFILE
echo 'set grid'                                                          >> $GNUPLOTFILE
echo 'set xlabel "Initialized '$MODEL_ASCIITIME' UTC"'                   >> $GNUPLOTFILE
echo 'set ylabel "Solar Radiation (W/m**2)"'                             >> $GNUPLOTFILE
echo 'set title "Global Solar Radiation Observed vs Forecast"'           >> $GNUPLOTFILE
echo 'set key left box'                                                  >> $GNUPLOTFILE
echo 'plot '$DQUOTE$FILE$DQUOTE' using 1:3 title "Forecast" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
echo        $DQUOTE$FILE$DQUOTE' using 1:4 title "Observed" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
echo        $DQUOTE$FILE$DQUOTE' using 1:5 title "RMS"      with linespoints lw 3 pt 5 ps 1'    >> $GNUPLOTFILE


# Run gnuplot
gnuplot < $GNUPLOTFILE

echo " "
echo "outputs..."
ls -l $GNUPLOTFILE
cd $PNGDIR
ls -l $PNGFILE                          

rm -f          $VAR_LC\_$TYPE\_fcst.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst.png

rm -f          $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png

#convert -resize 90x60! $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM\_thumb.png

echo " "
echo "outputs (old)..."
cp $PNGFILE $LOGDIR/cloud_fcst.png
ls -l $LOGDIR/cloud_fcst.png

