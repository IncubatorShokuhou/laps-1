#!@CSH@

setenv LAPSINSTALLROOT @prefix@
setenv LAPS_DATA_ROOT $1
setenv LOGDIR $LAPS_DATA_ROOT/log

# parse cloud logfiles to make a temporary data file for input to gnuplot
# this could eventually be done from the WGI script so we just grep one log file at a time
setenv FILE $LOGDIR/cloud_gp.log
echo "parsed log file = $FILE"
cd $LOGDIR
grep gnuplot cloud.wgi.* | cut -c22-100 > $FILE

# obtain xrange times from the data file
setenv STARTTIME1 `head -1 $FILE | cut -c1-11` ; echo $STARTTIME1
setenv STARTTIME2 `head -1 $FILE | cut -c13-23`; echo $STARTTIME2
setenv STOPTIME1  `tail -1 $FILE | cut -c1-11` ; echo $STOPTIME1
setenv STOPTIME2  `tail -1 $FILE | cut -c13-23`; echo $STOPTIME2

# Construct gnuplot .gp file
setenv GNUPLOTFILE $LOGDIR/cloud_analysis.gp
setenv DQUOTE \"
echo 'set terminal png size 1200,800'                    > $GNUPLOTFILE
echo 'set xdata time'                                   >> $GNUPLOTFILE
echo 'set timefmt "%d-%b-%Y %H:%M:%S"'                  >> $GNUPLOTFILE
echo 'set output '$DQUOTE$LOGDIR'/load.png'$DQUOTE      >> $GNUPLOTFILE
echo '' >> $GNUPLOTFILE
echo '# time range must be in same format as data file' >> $GNUPLOTFILE
echo 'set xrange ['$DQUOTE$STARTTIME1' '$STARTTIME2$DQUOTE':'$DQUOTE$STOPTIME1' '$STOPTIME2$DQUOTE']' >> $GNUPLOTFILE
echo 'set yrange [0:1000]'                              >> $GNUPLOTFILE
echo 'set grid'                                         >> $GNUPLOTFILE
echo 'set datafile missing "-99.9"'                     >> $GNUPLOTFILE
echo 'set xlabel "Date Time"'                           >> $GNUPLOTFILE
echo 'set ylabel "Solar Radiation (W/m**2)"'            >> $GNUPLOTFILE
echo 'set title "Global Solar Radiation Observed vs Analyzed"' >> $GNUPLOTFILE
echo 'set key left box'                                 >> $GNUPLOTFILE
echo 'set style line 1 lt 1 lw 3 pt 5 ps 1.0'           >> $GNUPLOTFILE
echo 'set style line 2 lt 2 lw 3 pt 5 ps 1.0'           >> $GNUPLOTFILE
echo 'set style line 3 lt 3 lw 3 pt 5 ps 1.0'           >> $GNUPLOTFILE
echo 'set style line 4 lt 4 lw 3 pt 7 ps 1.0'           >> $GNUPLOTFILE
echo 'plot '$DQUOTE$FILE$DQUOTE' using 1:3 title "Analyzed"  with linespoints ls 1          , \' >> $GNUPLOTFILE
echo        $DQUOTE$FILE$DQUOTE' using 1:4 title "Observed"  with linespoints ls 2          , \' >> $GNUPLOTFILE
echo        $DQUOTE$FILE$DQUOTE' using 1:5 title "RMS"       with linespoints ls 3          , \' >> $GNUPLOTFILE
echo        $DQUOTE$FILE$DQUOTE' using 1:6 title "Clear sky" with      points ls 4             ' >> $GNUPLOTFILE


# Run gnuplot
gnuplot < $GNUPLOTFILE

echo " "
echo "outputs..."  
ls -l $GNUPLOTFILE
ls -l $LOGDIR/load.png

# Create summary file
cd $LOGDIR
touch solar.log 
foreach wgi (cloud.wgi.*)
    echo " "                                       >> solar.log     
    echo " $wgi"                                   >> solar.log
    grep bias $wgi | grep -v residuals             >> solar.log
    grep means $wgi                                >> solar.log
    grep sensitivity $wgi                          >> solar.log
#   echo "    Sta   i    j   VIS frac tb8_k  t_gnd_k t_sfc_k cv_s_mx cvr_mx solalt 9pt  rad_an rad_ob rad_th ratio cv_sol  df" >> solar.log
#   grep sv $log | grep -e ' \*' -e ' +' -e ' - '  >> solar.log
end 
