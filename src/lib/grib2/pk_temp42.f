      subroutine pk_temp42(kfildo,ipack,nd5,is4,ns4,l3264b,
     1                     locn,ipos,ier,*)
c
c        march   2000   lawrence   gsc/tdl   original coding
c        january 2001   glahn      comments; ier = 403 changed to 402
c
c        purpose
c            packs template 4.2 into product definition section 
c            of a grib2 message.  template 4.2 is for
c            a derived forecast based on all ensemble members
c            at a horizontal level or in a horizontal layer
c            at a point in time. it is the responsibility of
c            the calling routine to pack the first 9 octets 
c            in section 4.
c
c        data set use
c           kfildo - unit number for output (print) file. (output)
c
c        variables
c              kfildo = unit number for output (print) file. (input)
c            ipack(j) = the array that holds the actual packed message
c                       (j=1,nd5). (input/output)
c                 nd5 = the size of the array ipack( ). (input)
c              is4(j) = contains the product definition information 
c                       for template 4.2 (in elements 10 through 36)
c                       that will be packed into ipack( ) (j=1,ns4).
c                       (input)
c                 ns4 = size of is4( ). (input) 
c              l3264b = the integer word length in bits of the machine
c                       being used. values of 32 and 64 are
c                       accommodated. (input)
c                locn = the word position to place the next value.
c                       (input/output)
c                ipos = the bit position in locn to start placing
c                       the next value. (input/output)
c                 ier = return status code. (output)
c                         0 = good return.
c                       1-4 = error codes generated by pkbg. see the 
c                             documentation in the pkbg routine.
c                       402 = is4( ) has not been dimensioned large
c                             enough to contain the entire template. 
c                   * = alternate return when ier ne 0. 
c
c             local variables
c             minsize = the smallest allowable dimension for is4( ).
c                   n = l3264b = the integer word length in bits of
c                       the machine being used. values of 32 and
c                       64 are accommodated.
c
c        non system subroutines called
c           pkbg
c
      parameter(minsize=36)
c
      dimension ipack(nd5), is4(ns4)
c
      n=l3264b
      ier=0
c
c        check the dimensions of is4( ).
c
      if(ns4.lt.minsize)then
c        write(kfildo,10)ns4,minsize
c10      format(/' is4( ) is currently dimensioned to contain'/
c    1           ' ns4=',i4,' elements. this array must be'/
c    2           ' dimensioned to at least ',i4,' elements'/
c    3           ' to contain all of the data in product'/
c    4           ' definition template 4.2.'/)
         ier=402
         go to 900
      endif
c
c        since this template shares the same information
c        as template 4.0, call the pk_temp40 routine
      call pk_temp40(kfildo,ipack,nd5,is4,ns4,l3264b,locn,ipos,
     1               ier,*900)
c
c        pack the derived forecast code 
      call pkbg(kfildo,ipack,nd5,locn,ipos,is4(35),8,n,ier,*900)
c
c        pack the number of forecasts in ensemble
      call pkbg(kfildo,ipack,nd5,locn,ipos,is4(36),8,n,ier,*900)
c
c       error return section
 900  if(ier.ne.0)return 1 
c
      return
      end
