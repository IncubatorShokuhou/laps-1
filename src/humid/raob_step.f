cdis    forecast systems laboratory
cdis    noaa/oar/erl/fsl
cdis    325 broadway
cdis    boulder, co     80303
cdis
cdis    forecast research division
cdis    local analysis and prediction branch
cdis    laps
cdis
cdis    this software and its documentation are in the public domain and
cdis    are furnished "as is."  the united states government, its
cdis    instrumentalities, officers, employees, and agents make no
cdis    warranty, express or implied, as to the usefulness of the software
cdis    and documentation for any purpose.  they assume no responsibility
cdis    (1) for the use of the software and documentation; or (2) to provide
cdis     technical support to users.
cdis
cdis    permission to use, copy, modify, and distribute this software is
cdis    hereby granted, provided that the entire disclaimer notice appears
cdis    in all copies.  all modifications to this software must be clearly
cdis    documented, and are solely the responsibility of the agent making
cdis    the modifications.  if significant modifications or enhancements
cdis    are made to this software, the fsl software policy manager
cdis    (softwaremgr@fsl.noaa.gov) should be notified.
cdis
cdis
cdis
cdis
cdis
cdis
cdis
      subroutine raob_step (i4time, data, laps_pressure, 
     1     raob_lookback,
     1     lat,lon,  ii,jj,kk)




c       this module modifies the input data array of specific humidity
c       with available raob data
c       this module will us .snd data if it is available.
c       in addition there is a way to turn off this module in the presense
c       of .snd data.   simply modify the static file
c       ../static/raob_moisture_switch.txt
c       by putting a 0 in the first recort.  the advantage of this approach
c       is that it is external to the program and will not require a
c       recompile.
c
c       this software was developed by dan birkenheuer, june 1996
c
c       theory:
c
c       this code utilizes the second pass of a barnes analysis.
c       in this form, a background is already available i.e., from the
c       model forecast that the moisture analysis obtains from .lga
c
c       the second pass barnes takes the background and adds to it the
c       weighted differences between the background and the raob observations
c       the difference "effects" are distributed over the entire domain
c       with the respective weights being a function of the distance from
c       the observation.
c
c       this is a "one-pass" analysis.
c
c       lambda in this code is hardwired to 600km.  see barnes for the
c       significance of lambda.
c
c       a note on the look_back_time parameter:
c
c       look_back_time was devised to allow the generation of snd files
c       only at the time of observation (not in the event of no obs)
c       hence this is really not required in the current way the "system"
c       as a whole is being coded as a group.  in the "group" approach,
c       a .snd file will be created each laps time and will automatically
c       contain the raob data for a predescribed "look back time".  typically
c       this will be 3 hours.  if there is no data, the .snd file will exist
c       and will be empty.
c
c       if for some reason this group approach for lookback time is ever
c       dropped or modified, this code contains a switch to accommodate that
c       scenario if needed.
c
c       on a related note, if the .snd files exist with a more recent date
c       than the current runtime, the presence of these will be detected
c       and the current runtime .snd file will be sought instead.  if this
c       current file is not avialable, the code will report it is
c       unable to find .snd data for the current time.


c       $log: raob_step.for,v $
c revision 1.8  1996/08/30  20:49:17  birk
c modified to interface to modified "switch" file
c
c revision 1.7  1996/07/30  15:34:05  birk
c added test for negative sh generated by barnes adjustment..
c now return 0.0 sh for this condition
c
c revision 1.6  1996/07/18  21:32:40  birk
c modified read statement to handle real instead of integer
c type for height level   required for .snd file compatibility
c
c revision 1.5  1996/07/10  19:55:14  birk
c fixed bug in checking for missing raob data (failed under aix)
c
c revision 1.4  1996/06/27  22:10:12  birk
c corrected error in missing file check
c
c revision 1.3  1996/06/25  19:27:34  birk
c added log line
c

      implicit none

c     include 'lapsparms.for'
c     include 'parmtrs.inc'


c     input parameters

      integer i4time, ii,jj,kk, raob_lookback
      real data(ii,jj,kk), laps_pressure (kk)
      real lat(ii, jj), lon(ii, jj)

c  dynamic dependent parameters                     

      real  q_r(kk,100)
      real diff(kk,100)
      real weight (ii,jj,100)

c  normal internal parameters

      integer i,j,k,ks,is
      integer look_back_time
      integer raob_i4time       ! actual time of raob data used
      integer numoffiles, istatus, maxfiles
      character*256 c_filenames(200),pathname_in
      character*9 filename
      character*200 fname
      integer idx, len
      real lat_r(100), lon_r(100)
      integer  i_r(100), j_r(100)
      integer lev_r(100), dummy, isound
      real p_r(70,100), td_r(70,100), t_r(70,100)
      real temt, temtd, ssh2
      real r, rmin
      real d2r,pi
      real tot_diff, tot_weight
      real rspacing_dum
      real rmd
      real W_cutoff             !weight cutoff
        
c *** begin routine

      pi = acos(-1.0)
      d2r = pi/180.
      look_back_time = raob_lookback
      maxfiles = 200
      call get_r_missing_data(rmd, istatus)   

c     *** read in the raob data

c     +++ get latest raob file name

      call get_directory('snd',pathname_in,len)
      pathname_in = pathname_in(1:len)//'*'
c     pathname_in = '../lapsprd/snd/*'

      call get_file_names (pathname_in,numoffiles,c_filenames,
     1     maxfiles,istatus)

      if (istatus.ne.1) then    ! no data to read
         return

      elseif(numoffiles.eq.0) then ! no data to read
         return

      endif

      idx = index(c_filenames(numoffiles), ' ')

      filename = c_filenames(numoffiles)(idx-13:idx-4)

      call i4time_fname_lp (filename, raob_i4time, istatus)

c     +++ validate the raob time

      if (raob_i4time + look_back_time .lt. i4time) then ! too old
         write (6,*) 'raob data found is too old to be used'
         return
      endif

      if (raob_i4time .gt. i4time) then ! too new
         write (6,*) 'warning raob data found is too new to be used'
         write (6,*) 'assigning current file time for .snd'
         raob_i4time = i4time
         call make_fnam_lp (raob_i4time, filename, istatus)
      endif

c     +++ read raob file
      call get_directory('snd',fname,len)

      do isound = 1,100
                
         open (12, file = fname(1:len)//filename//'.snd',
     1        form='formatted',status='old',err=18)
 15      read (12,*,end=16) dummy, idx, lat_r(isound), lon_r (isound)
         print*, idx
         lev_r(isound) = 1

         do i = 1, idx

            read(12,*) rspacing_dum,
     1           p_r (lev_r(isound),isound),
     1           t_r (lev_r(isound),isound),
     1           td_r(lev_r(isound),isound)

            if (
     1           (p_r (lev_r(isound),isound).ne.rmd)
     1           .and.
     1           (t_r (lev_r(isound),isound).ne.rmd)
     1           .and.
     1           (td_r (lev_r(isound),isound).ne.rmd)
     1           ) then

               lev_r(isound) = lev_r(isound) + 1

            else

               continue

            endif

         enddo

         lev_r(isound) = lev_r(isound) - 1

      enddo

 16   close (12)

      isound = isound -1

      if (isound .le. 0 ) then ! no data found
         write(6,*) 'No RAOB data available in database'
      else
         write(6,*) isound, ' Number of RAOBs considered in analysis'
      endif

c     +++ compute the nearest i,j for all sounding locations
c     also compute weighting function for all locations for given raob

      W_cutoff =  exp ( -1.*(.25 * 6371)**2/101131. )

      do k = 1,isound

         rmin = 1.e30

         do j = 1,jj
            do i = 1,ii

               r = sin(lat_r(k)*d2r)*sin(lat(i,j)*d2r)
     1              +cos(lat_r(k)*d2r) *cos(lat(i,j)*d2r)
     1              * cos( abs( lon_r(k) - lon(i,j)) *d2r )

               r = acos (r)

               if (r .ge. 0.25) then
                  weight(i,j,k) = W_cutoff
               else
                  weight(i,j,k) = exp ( -1.*(r * 6371)**2/101131. ) 
c     lam = 600 km
c     2*lam = 1200km , c = ln(2)*lam**2/4/pi**2 = 25282km**2
c     denominator = 4*c = 101131.
c     cutoff of 0.25 results in a weight of 1.275e-11, chosen with care
c     to eliminate circles.

               endif

c     rmin is the minimum distance to the raob (i,j of raob essentially)
               rmin = min (rmin,r)
               if (rmin.eq.r) then
                  i_r(k) = i
                  j_r(k) = j
               endif

            enddo
         enddo

      enddo

c     *** interpolate q in the vertical at each raob location to the laps
c     pressure levels

      do is = 1,isound

         do k = 1,kk            ! each laps pressure level (ks)

            call locate(p_r(1,is),lev_r(is),laps_pressure(k),ks)

            if(ks.eq.0  .or. ks.eq.lev_r(is)) then

               q_r(k,is) = rmd

            else                ! interpolate to the desired level

               call interp (log(laps_pressure(k)),
     1              log(p_r(ks,is)),log(p_r(ks+1,is)),
     1              t_r(ks,is),t_r(ks+1,is),temt)

               call interp (log(laps_pressure(k)),
     1              log(p_r(ks,is)),log(p_r(ks+1,is)),
     1              td_r(ks,is),td_r(ks+1,is),temtd)

               q_r(k,is) = ssh2 (laps_pressure(k),temt,temtd,0.0)/1000.

            endif

         enddo                  ! k

      enddo                     ! is

c *** difference the raob data at each gridpoint location and height

      do is = 1,isound

         do k = 1,kk

            if (
     1           (data(i_r(is),j_r(is),k) .eq. rmd)
     1           .or.
     1           (q_r(k,is) .eq. rmd)
     1           ) then

               diff(k,is) = rmd

            else

               diff(k,is) = q_r(k,is) - data(i_r(is),j_r(is),k)

            endif

         enddo

      enddo

c     *** analyze the field (barnes second pass to background)

      do k = 1,kk
         do j = 1,jj
            do i = 1,ii

               tot_weight = 0.0
               tot_diff = 0.0

               do is = 1,isound

                  if (diff(k,is).eq.rmd) then !dont modify
                     continue
                  elseif(data(i,j,k).eq.rmd) then !dont modify
                     continue
                  else
                     tot_diff = weight(i,j,is)*diff(k,is) + tot_diff
                     tot_weight = tot_weight + weight(i,j,is)
                  endif

               enddo

               if (
     1              (data(i,j,k) .ne. rmd)
     1              .and.
     1              (tot_weight.ne.0.0) )then
                  data(i,j,k) = data(i,j,k) + tot_diff/tot_weight

                  if (data(i,j,k) .lt. 0.0) data(i,j,k) = 0.0
               endif

            enddo
         enddo
      enddo

c     *** return modified data array

      write(6,*) 'end RAOB step'

      return

 18   write(6,*) 'error opening requested .snd file, raob step skipped'
      return

 24   write(6,*)  'raob moisture switch missing!, ignoring raobs'
      return

      end
