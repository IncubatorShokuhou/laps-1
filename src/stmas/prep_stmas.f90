MODULE PREP_STMAS4D

  USE PRMTRS_STMAS

CONTAINS

SUBROUTINE PREPROCSS
!*************************************************
! DATA PREPROCESS
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
  CALL GRDMEMALC
  CALL GTSCALING
  RETURN
END SUBROUTINE PREPROCSS

SUBROUTINE GRDMEMALC
!*************************************************
! MEMORY ALLOCATE FOR GRD ARRAY
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S,ER
! --------------------
  ALLOCATE(COR(NUMGRID(1),NUMGRID(2)),STAT=ER)
  IF(ER.NE.0)STOP 'COR ALLOCATE WRONG'
  ALLOCATE(XXX(NUMGRID(1),NUMGRID(2)),STAT=ER)
  IF(ER.NE.0)STOP 'XXX ALLOCATE WRONG'
  ALLOCATE(YYY(NUMGRID(1),NUMGRID(2)),STAT=ER)
  IF(ER.NE.0)STOP 'YYY ALLOCATE WRONG'
  ALLOCATE(DEG(NUMGRID(1),NUMGRID(2)),STAT=ER)
  IF(ER.NE.0)STOP 'DEG ALLOCATE WRONG'
  ALLOCATE(DEN(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4)),STAT=ER)
  IF(ER.NE.0)STOP 'DEN ALLOCATE WRONG'
  IF(IFPCDNT.EQ.0)THEN
    ALLOCATE(ZZZ(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4)),STAT=ER)
    IF(ER.NE.0)STOP 'ZZZ ALLOCATE WRONG'
  ELSEIF(IFPCDNT.EQ.1)THEN
    ALLOCATE(PPP(NUMGRID(3)),STAT=ER)
    IF(ER.NE.0)STOP 'PPP ALLOCATE WRONG'
  ENDIF
  ALLOCATE(GRDBKGND(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4),NUMSTAT),STAT=ER)
  IF(ER.NE.0)STOP 'GRDBKGND ALLOCATE WRONG'
  ALLOCATE(GRDANALS(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4),NUMSTAT),STAT=ER)
  IF(ER.NE.0)STOP 'GRDANALS ALLOCATE WRONG'
  DO T=1,NUMGRID(4)
  DO K=1,NUMGRID(3)
  DO J=1,NUMGRID(2)
  DO I=1,NUMGRID(1)
    DO S=1,NUMSTAT
      GRDANALS(I,J,K,T,S)=0.0D0
      GRDBKGND(I,J,K,T,S)=0.0D0
    ENDDO
  ENDDO
  ENDDO
  ENDDO
  ENDDO
  NUMVARS=NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4)*NUMSTAT
  ALLOCATE(GRADINT(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4),NUMSTAT),STAT=ER)
  IF(ER.NE.0)STOP 'GRADINT ALLOCATE WRONG'
  RETURN
END SUBROUTINE GRDMEMALC

SUBROUTINE GTSCALING
!*************************************************
! ALLOCATE OBSERVATION MEMORY AND READ IN DATA AND SCALE
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  REAL(KIND=8),PARAMETER :: SM=1.0E-5
  INTEGER(KIND=4) :: O,S,ER
! --------------------
! READ IN OBSERVATIONS
  ALLOCATE(NOBSTAT(NUMSTAT),STAT=ER)
  IF(ER.NE.0)STOP 'NOBSTAT ALLOCATE WRONG'
  ALLOCATE(SCL(NUMSTAT),STAT=ER)
  IF(ER.NE.0)STOP 'SCL ALLOCATE WRONG'
  DO S=1,NUMSTAT
    NOBSTAT(S)=0
    SCL(S)=0.0D0
  ENDDO
  DO O=1,NALLOBS
    S=OBSSTATE(O)
    IF(S.LE.NUMSTAT)THEN
      SCL(S)=SCL(S)+OBSVALUE(O)*OBSVALUE(O)
      NOBSTAT(S)=NOBSTAT(S)+1
    ELSEIF(S.EQ.NUMSTAT+1)THEN  ! FOR RADAR
      SCL(S-NUMSTAT)=SCL(S-NUMSTAT)+OBSVALUE(O)*OBSVALUE(O)
      NOBSTAT(S-NUMSTAT)=NOBSTAT(S-NUMSTAT)+1
      SCL(S-NUMSTAT+1)=SCL(S-NUMSTAT+1)+OBSVALUE(O)*OBSVALUE(O)
      NOBSTAT(S-NUMSTAT+1)=NOBSTAT(S-NUMSTAT+1)+1
    ENDIF
  ENDDO
  DO S=1,NUMSTAT
    IF(NOBSTAT(S).GT.0)SCL(S)=DSQRT(SCL(S)/NOBSTAT(S))
    IF(NOBSTAT(S).EQ.0)SCL(S)=SL0(S)
  ENDDO
  IF(NUMDIMS.GE.2)SCL(1)=DMAX1(SCL(1),SCL(2))
  IF(NUMDIMS.GE.2)SCL(2)=SCL(1)
! SCALE THE OBSERVATIONS
  DO O=1,NALLOBS
    S=OBSSTATE(O)
    IF(S.LE.NUMSTAT)THEN
      OBSVALUE(O)=OBSVALUE(O)/SCL(S)
      OBSERROR(O)=OBSERROR(O)/SCL(S)
    ELSEIF(S.EQ.NUMSTAT+1)THEN  ! FOR RADAR
      OBSVALUE(O)=OBSVALUE(O)/SCL(1)
      OBSERROR(O)=OBSERROR(O)/SCL(1)
    ENDIF
  ENDDO
  RETURN
END SUBROUTINE GTSCALING

SUBROUTINE RDINITBGD
!*************************************************
! READ IN INITIAL BACKGROUND FIELDS
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S,NX,NY,NZ,NT,I1,J1,K1,T1
! --------------------
  IF(NUMGRID(1).GE.2)THEN
    NX=(MAXGRID(1)-1)/(NUMGRID(1)-1)
  ELSE
    NX=1
  ENDIF
  IF(NUMGRID(2).GE.2)THEN
    NY=(MAXGRID(2)-1)/(NUMGRID(2)-1)
  ELSE
    NY=1
  ENDIF
  IF(NUMGRID(3).GE.2)THEN
    NZ=(MAXGRID(3)-1)/(NUMGRID(3)-1)
  ELSE
    NZ=1
  ENDIF
  IF(NUMGRID(4).GE.2)THEN
    NT=(MAXGRID(4)-1)/(NUMGRID(4)-1)
  ELSE
    NT=1
  ENDIF
  DO T=1,MAXGRID(4),NT
  DO K=1,MAXGRID(3),NZ
  DO J=1,MAXGRID(2),NY
  DO I=1,MAXGRID(1),NX
    I1=(I-1)/NX+1
    J1=(J-1)/NY+1
    K1=(K-1)/NZ+1
    T1=(T-1)/NT+1
    DO S=1,NUMSTAT
      GRDBKGND(I1,J1,K1,T1,S)=GRDBKGD0(I,J,K,T,S)/SCL(S)
    ENDDO
    DEN(I1,J1,K1,T1)=DN0(I,J,K,T)
  ENDDO
  ENDDO
  ENDDO
  ENDDO
  RETURN
END SUBROUTINE RDINITBGD

SUBROUTINE PHYSCPSTN
!*************************************************
! DEALING WITH PHYSICAL POSITION AND PENALTY COEFFICENT AND MAKING SOME SCALING
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S,NX,NY,NZ,NT,I1,J1,K1,T1
  REAL(KIND=8)    :: Z1,Z2
! --------------------
! PHYSICAL POSITION AND CORIOLIS FREQUENCY
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
  ENDDO
  ENDDO
  IF(NUMGRID(1).GE.2)THEN
    NX=(MAXGRID(1)-1)/(NUMGRID(1)-1)
  ELSE
    NX=1
  ENDIF
  IF(NUMGRID(2).GE.2)THEN
    NY=(MAXGRID(2)-1)/(NUMGRID(2)-1)
  ELSE
    NY=1
  ENDIF
  DO J=1,MAXGRID(2),NY
  DO I=1,MAXGRID(1),NX
    I1=(I-1)/NX+1
    J1=(J-1)/NY+1
    IF(NUMGRID(1).GE.2)XXX(I1,J1)=XX0(I,J)
    IF(NUMGRID(2).GE.2)YYY(I1,J1)=YY0(I,J)
    IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)COR(I1,J1)=CR0(I,J)
    IF(NUMGRID(2).GE.2)DEG(I1,J1)=DG0(I,J)
  ENDDO
  ENDDO
  IF(IFPCDNT.EQ.0)THEN
    IF(NUMGRID(1).GE.2)THEN
      NX=(MAXGRID(1)-1)/(NUMGRID(1)-1)
    ELSE
      NX=1
    ENDIF
    IF(NUMGRID(2).GE.2)THEN
      NY=(MAXGRID(2)-1)/(NUMGRID(2)-1)
    ELSE
      NY=1
    ENDIF
    IF(NUMGRID(3).GE.2)THEN
      NZ=(MAXGRID(3)-1)/(NUMGRID(3)-1)
    ELSE
      NZ=1
    ENDIF
    IF(NUMGRID(4).GE.2)THEN
      NT=(MAXGRID(4)-1)/(NUMGRID(4)-1)
    ELSE
      NT=1
    ENDIF
    DO T=1,MAXGRID(4),NT
    DO K=1,MAXGRID(3),NZ
    DO J=1,MAXGRID(2),NY
    DO I=1,MAXGRID(1),NX
      I1=(I-1)/NX+1
      J1=(J-1)/NY+1
      K1=(K-1)/NZ+1
      T1=(T-1)/NT+1
      ZZZ(I1,J1,K1,T1)=ZZ0(I,J,K,T)
    ENDDO
    ENDDO
    ENDDO
    ENDDO
  ELSEIF(IFPCDNT.EQ.1)THEN
    IF(NUMGRID(3).GE.2)THEN
      NZ=(MAXGRID(3)-1)/(NUMGRID(3)-1)
    ELSE
      NZ=1
    ENDIF
    DO K=1,MAXGRID(3),NZ
      K1=(K-1)/NZ+1
      PPP(K1)=PP0(K)
    ENDDO
  ENDIF
! PENALTY COEFFICENT
  DO S=1,NUMSTAT
    PENAL_X(S)=PENAL0X(S)*GRDSPAC(1)*GRDSPAC(1)*GRDSPAC(1)*GRDSPAC(1)*SCL(S)*SCL(S) !&
!              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
    PENAL_Y(S)=PENAL0Y(S)*GRDSPAC(2)*GRDSPAC(2)*GRDSPAC(2)*GRDSPAC(2)*SCL(S)*SCL(S) !&
!              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
    PENAL_Z(S)=PENAL0Z(S)*SCL(S)*SCL(S) !&
!              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
    PENAL_T(S)=PENAL0T(S)*GRDSPAC(4)*GRDSPAC(4)*GRDSPAC(4)*GRDSPAC(4)*SCL(S)*SCL(S) !&
!              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
  ENDDO
  PNLT_PU=PNLT0PU*SCL(1)*SCL(1)*NALLOBS/NUMSTAT/(2**(GRDLEVL-1)) &
              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
  PNLT_PV=PNLT0PV*SCL(2)*SCL(2)*NALLOBS/NUMSTAT/(2**(GRDLEVL-1)) &
              /(NUMGRID(1)*NUMGRID(2)*NUMGRID(3)*NUMGRID(4))
  IF(NOBSTAT(1).EQ.0.AND.NOBSTAT(3).EQ.0)PNLT_PU=0.0D0
  IF(NOBSTAT(2).EQ.0.AND.NOBSTAT(3).EQ.0)PNLT_PV=0.0D0
! CALCULATE SCALE FOR PHYSICAL POSITION AND CORIOLIS FREQUENCY
  SCP(1)=DABS((XXX(NUMGRID(1),NUMGRID(2))-XXX(1,1))/(NUMGRID(1)-1))
  SCP(2)=DABS((YYY(NUMGRID(1),NUMGRID(2))-YYY(1,1))/(NUMGRID(2)-1))
  IF(IFPCDNT.EQ.0)THEN
    Z2=ZZZ(1,1,NUMGRID(3),1)
    Z1=ZZZ(1,1,1,1)
    DO I=1,NUMGRID(1)
    DO J=1,NUMGRID(2)
    DO T=1,NUMGRID(4)
      Z2=DMAX1(Z2,ZZZ(I,J,NUMGRID(3),T))
      Z1=DMIN1(Z1,ZZZ(I,J,1,T))
    ENDDO
    ENDDO
    ENDDO
    SCP(3)=DABS((Z2-Z1)/(NUMGRID(3)-1))
  ELSEIF(IFPCDNT.EQ.1)THEN
    SCP(3)=DABS((PPP(NUMGRID(3))-PPP(1))/(NUMGRID(3)-1))
  ENDIF
  IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)SCP(4)=DABS(COR(1,1))
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)SCP(4)=DMAX1(SCP(4),DABS(COR(I,J)))
  ENDDO
  ENDDO
  IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)SCP(5)=DABS(DEN(1,1,1,1))
  DO T=1,NUMGRID(4)
  DO K=1,NUMGRID(3)
  DO J=1,NUMGRID(2)
  DO I=1,NUMGRID(1)
    IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)SCP(5)=DMAX1(SCP(5),DABS(DEN(I,J,K,T)))
  ENDDO
  ENDDO
  ENDDO
  ENDDO
! SCALE THE PHYSICAL POSITION AND CORIOLIS FREQUENCY
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(NUMGRID(1).GE.2)XXX(I,J)=(XXX(I,J)-ORIPSTN(1))/SCP(1)
    IF(NUMGRID(2).GE.2)YYY(I,J)=(YYY(I,J)-ORIPSTN(2))/SCP(2)
    IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)COR(I,J)=COR(I,J)/SCP(4)
  ENDDO
  ENDDO
  DO T=1,NUMGRID(4)
  DO K=1,NUMGRID(3)
  DO J=1,NUMGRID(2)
  DO I=1,NUMGRID(1)
    IF(PNLT0PU.GE.1.0E-10.OR.PNLT0PV.GE.1.0E-10)DEN(I,J,K,T)=DEN(I,J,K,T)/SCP(5)
  ENDDO
  ENDDO
  ENDDO
  ENDDO
  IF(IFPCDNT.EQ.0)THEN
    ORIVTCL=ZZZ(1,1,1,1)
    DO T=1,NUMGRID(4)
    DO K=1,NUMGRID(3)
    DO J=1,NUMGRID(2)
    DO I=1,NUMGRID(1)
      ZZZ(I,J,K,T)=(ZZZ(I,J,K,T)-ORIVTCL)/SCP(3)
    ENDDO
    ENDDO
    ENDDO
    ENDDO
  ELSEIF(IFPCDNT.EQ.1)THEN
    ORIVTCL=PPP(1)
    DO K=1,NUMGRID(3)
      PPP(K)=(PPP(K)-ORIVTCL)/SCP(3)
    ENDDO
  ENDIF
  RETURN
END SUBROUTINE PHYSCPSTN

END MODULE PREP_STMAS4D
