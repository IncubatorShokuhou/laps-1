MODULE READ_BACKGRD
!*************************************************
! READ IN BACKGROUND INFORMATIONS
! HISTORY: JANUARY 2008, DIVORCED FROM INPUT_BG_OBS MODULE by ZHONGJIE HE.
!*************************************************

  USE PRMTRS_STMAS
  USE DRAWCOUNTOUR ,  ONLY : DRCONTOUR, DRCONTOUR_2D

  PUBLIC    TPG, BK0, C00, D00, S00, X00, Y00, T00, P00, Z00, DX0, DY0, DZ0, DT0, HEIGHTU, HEIGHTL
  PUBLIC    RDLAPSBKG, RDBCKGRND, ALLOCTBKG, DEALCTBKG, RDBKGTEST

  REAL                  :: DX0,DY0
  REAL     ,ALLOCATABLE :: X00(:,:),Y00(:,:),T00(:),P00(:),Z00(:), DZ0(:,:,:)
  REAL     ,ALLOCATABLE :: TPG(:,:),BK0(:,:,:,:,:),C00(:,:),D00(:,:),S00(:,:,:,:)
  REAL     ,ALLOCATABLE :: HEIGHTU(:,:),HEIGHTL(:,:)

! ===========ADDED BY ZHONGJIE HE JUST FOR DRAW PICTURES.
  PUBLIC    ORI_LON, ORI_LAT, END_LON, END_LAT
  REAL                  :: ORI_LON, ORI_LAT, END_LON, END_LAT
! =======================================================

!***************************************************
!!COMMENT:
!   THIS MODULE IS MAINLY USED BY THE MODULE OF INPUT_BG_OBS, THE AIM IS TO READ BACKGROUND INFORMATIONS FROM MODELS OR SOME INITIAL FIELDS.
!   SUBROUTINES:
!      ALLOCTBKG: MEMORY ALLOCATE FOR X00, Y00, P00, Z00, TPG, BK0, C00, D00, S00.
!      DEALCTBKG: MEMORY DEALLOCATE FOR X00, Y00, P00, Z00, TPG, BK0, C00, D00, S00. 
!      RDLAPSBKG: READ IN BACKGROUND FROM LAPS SYSTEM.
!      RDBCKGRND: READ IN BACKGROUND FROM DATA FILES OF 'FORT.11'.
!      RDBKGTEST: JUST A TEMPORARY SUBROUTINE TO CONSTRUCTION A BACKGROUND FOR A TEST CASE.
!   ARRAYS:
!      TPG: TOPOGRAPHY
!      BK0: BACKGROUND FIELDS ON THE ORIGINAL GRID POINTS (SUCH AS MODELS OR LAPS SYSTEMS).
!      C00: CORIOLIS FORCE ON THE ORIGINAL GIRD.
!      D00: ROTATE ANGLE'S OF THE COORDINATE TO THE EAST-NORTH COORDINATE. (GUESSED BY ZHONGJIE HE)
!      S00: DENSITY ON THE ORIGINAL GRID. 
!      X00: LONGITUDE OF THE ORIGINAL GRID POINTS.
!      Y00: LATITUDE OF THE ORIGINAL GRID POINTS.
!      P00: PRESURES AT EACH LEVEL
!      Z00: HEIGHT OF EACH LEVEL
!      DX0: GRID SPACING IN X DIRECTION, UNIT IS METER 
!      DY0: GRID SPACING IN Y DIRECTION, UNIT IS METER
!   VARIABLES:
!      ORI_LON: LONGITUDE OF THE EAST BOUNDARY OF THE AREA
!      ORI_LAT: LATITUDE OF THE SOUTH BOUNDARY OF THE AREA
!      END_LON: LONGITUDE OF THE WEST BOUNDARY OF THE AREA
!      END_LAT: LATITUDE OF THE NORTH BOUNDARY OF TEH AREA
CONTAINS

SUBROUTINE ALLOCTBKG
!*************************************************
! MEMORY ALLOCATE FOR X00, Y00, P00, Z00, TPG, BK0, C00, D00, S00
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!          OCTOBER   2007, by YUANFU XIE (USE LAPS)
!          JANUARY   2008, DIVORCED FROME MEMORYALC SUBROUTINE BY ZHONGJIE HE
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER  :: ER
! --------------------
  ALLOCATE(BK0(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4),NUMSTAT+3),STAT=ER)
  IF(ER.NE.0)STOP 'BK0 ALLOCATE WRONG'
  ALLOCATE(X00(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'X00 ALLOCATE WRONG'
  ALLOCATE(Y00(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'Y00 ALLOCATE WRONG'
  ALLOCATE(TPG(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'TPG ALLOCATE WRONG'
  ALLOCATE(C00(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'C00 ALLOCATE WRONG'
  ALLOCATE(D00(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'D00 ALLOCATE WRONG'
  ALLOCATE(P00(FCSTGRD(3)),STAT=ER)
  IF(ER.NE.0)STOP 'P00 ALLOCATE WRONG'
  ALLOCATE(S00(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4)),STAT=ER)
  IF(ER.NE.0)STOP 'S00 ALLOCATE WRONG'
  ALLOCATE(Z00(FCSTGRD(3)),STAT=ER)
  IF(ER.NE.0)STOP 'Z00 ALLOCATE WRONG'
  ALLOCATE(DZ0(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)),STAT=ER)
  IF(ER.NE.0)STOP 'DZ0 ALLOCATE WRONG'
  ALLOCATE(HEIGHTU(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'HEIGHTU ALLOCATE WRONG'
  ALLOCATE(HEIGHTL(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF(ER.NE.0)STOP 'HEIGHTL ALLOCATE WRONG'
  ALLOCATE(T00(FCSTGRD(4)),STAT=ER)
  IF(ER.NE.0)STOP 'T00 ALLOCATE WRONG'

  ! ALLOCATE SPACE FOR LAPS LAT/LON/TOPOGRAPHY GRIDS (YUANFU):
  ALLOCATE(LATITUDE(FCSTGRD(1),FCSTGRD(2)),LONGITUD(FCSTGRD(1),FCSTGRD(2)), &
             TOPOGRPH(FCSTGRD(1),FCSTGRD(2)),STAT=ER)
  IF (ER.NE.0) THEN
    PRINT*,'ALLOCTBKG: cannot allocate memory for laps lat/lon/topography'
    STOP
  ENDIF

  RETURN
END SUBROUTINE ALLOCTBKG

SUBROUTINE DEALCTBKG
!*************************************************
! MEMORY DEALLOCATE FOR X00, Y00, P00, Z00, TPG, BK0, C00, D00, S00
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!          OCTOBER   2007, by YUANFU XIE (USE LAPS)
!          JANUARY   2008, SEPARATED FROME MEMORYALC SUBROUTINE BY ZHONGJIE HE
!*************************************************
  IMPLICIT NONE
! --------------------
  ! DEALLOCATE(BK0)		FOR KEEPING THE BACKGROUND TO THE END BY YUANFU
  DEALLOCATE(X00)
  DEALLOCATE(Y00)
  DEALLOCATE(TPG)
  DEALLOCATE(C00)
  DEALLOCATE(D00)
  DEALLOCATE(P00)
  DEALLOCATE(S00)
  DEALLOCATE(Z00)
  DEALLOCATE(DZ0)
  DEALLOCATE(HEIGHTU)
  DEALLOCATE(HEIGHTL)
  DEALLOCATE(T00)

  ! DEALLOCATE LAPS VARIABLES:
  IF (IF_TEST .NE. 0) DEALLOCATE(LATITUDE, LONGITUD, TOPOGRPH)

  RETURN
END SUBROUTINE DEALCTBKG

SUBROUTINE RDLAPSBKG
!*************************************************
! READ IN BACKGROUND FROM LAPS SYSTEM
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!          OCTOBER   2007, by YUANFU XIE (USE LAPS)
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER  :: I,J,K,T,LN
  CHARACTER(LEN=200) :: DR
  REAL  :: UU(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: VV(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: ZZ(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: TT(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: QQ(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: OX,OY,EX,EY

  CHARACTER*4  :: VN            ! VARNAME ADDED BY YUANFU
  CHARACTER*9  :: TM            ! ASCII TIME ADDED BY YUANFU
  INTEGER      :: ST            ! STATUS OF LAPS CALLS ADDED BY YUANFU
  INTEGER*4    :: I4            ! SYSTEM I4 TIME ADDED BY YUANFU
  ! REAL         :: HT(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)), &
  !                 T3(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)), &
  !                 U3(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)), &
  !                 V3(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)), &
  !                 SH(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3)), &
  REAL         :: LA(FCSTGRD(1),FCSTGRD(2)),LO(FCSTGRD(1),FCSTGRD(2)), &
                  AG(FCSTGRD(1),FCSTGRD(2)),P1(FCSTGRD(3)),DS(2)

  integer :: mxi,mxj,mxk,mxt
  real :: amx,hy(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3))
  REAL :: D2R
  D2R = 3.14159/180.0

!DECLARE:
!        'HT' AND 'ZZ' ARE THE HEIGHT AT EVERY GRIDPOINT
!        'T3' AND 'TT' ARE THE TEMPETURE AT EVERY GRIDPOINT
!        'U3' AND 'UU' ARE THE WEST-EAST VELOCITY AT EACH GRIDPOINT
!        'V3' AND 'VV' ARE THE SOUTH-NORTH VELOCITY AT EACH GRIDPOINT
!        'LA' AND 'LO' ARE RESPECTIVELY THE LATITUDE AND LONGITUDE OF EACH GRIDPOINT
!        'P1' IS THE PRESSURE OF EACH LEVEL
! --------------------

  IF(IFPCDNT.NE.1) THEN              !   BY ZHONGJIEHE
    PRINT*, 'ERROR! LAPS IS PRESSURE COORDINATE! PLEASE SET IFPCDNT TO 1!'
    STOP
  ENDIF                              !   END OF MODIFIED BY ZHONGJIE HE

  ! LAPS CYCLE/SYSTEM TIMES:
  ! CALL GET_SYSTIME(ITIME2(2),TM,ST)	! MOVE TO LAPS_CONFIG
  ! CALL GET_LAPS_CYCLE_TIME(ICYCLE,ST)! MOVE TO LAPS_CONFIG

  ! STMAS TIME WINDOW TEMPORARILY USES (-0.5CYCLE, 0.5*CYCLE):
  !ITIME2(1) = ITIME2(2)-(FCSTGRD(4)-1)*ICYCLE
  ITIME2(1) = LAPSI4T-ICYCLE/2
  ITIME2(2) = LAPSI4T+ICYCLE/2

  ! USE LAPS INGEST TO READ BACKGROUND FIELDS: ADDED BY YUANFU

  ! CURRENTLY ONLY WORKING FOR FCSTGRD(4)=3:
  IF (FCSTGRD(4) .NE. 3) THEN
    PRINT*,'Currently, STMAS 3D has been tested only for 3 time frames!'
    PRINT*,'Change the RDLAPSBKG code for an analysis with different time frames!'
    STOP
  ENDIF

  ! BACKGROUND TIME:
  DO T=1,FCSTGRD(4)
    T00(T) = (T-1)/FLOAT(FCSTGRD(4)-1)*(ITIME2(2)-ITIME2(1))
  ENDDO

  DO T=1,2			! READ IN PREVIOUS TWO TIME FRAMES
    ! SYSTEM TIME:
    I4 = LAPSI4T+(T-2)*ICYCLE	! USE HALF LAPS TIME FRAMES BY YUANFU

    ! HEIGHT FIELD:
    VN = 'HT'
    CALL GET_MODELFG_3D(I4,VN,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),ZZ(1,1,1,T),ST)
    IF (ST .NE. 1) THEN
      PRINT*,'RDLAPSBKG: No HT background available at time frame: ',T
      STOP
    ENDIF
    ! TEMPERATURE FIELD:
    VN = 'T3'
    CALL GET_MODELFG_3D(I4,VN,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),TT(1,1,1,T),ST)
    IF (ST .NE. 1) THEN
      PRINT*,'RDLAPSBKG: No T3 background available at time frame: ',T
      STOP
    ENDIF
    ! WIND U:
    VN = 'U3'
    CALL GET_MODELFG_3D(I4,VN,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),UU(1,1,1,T),ST)
    IF (ST .NE. 1) THEN
      PRINT*,'RDLAPSBKG: No U3 background available at time frame: ',T
      STOP
    ENDIF
    ! WIND V:
    VN = 'V3'
    CALL GET_MODELFG_3D(I4,VN,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),VV(1,1,1,T),ST)
    IF (ST .NE. 1) THEN
      PRINT*,'RDLAPSBKG: No V3 background available at time frame: ',T
      STOP
    ENDIF
    ! SPECIFIC HUMIDITY:
    VN = 'SH'
    CALL GET_MODELFG_3D(I4,VN,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),QQ(1,1,1,T),ST)
    IF (ST .NE. 1) THEN
      PRINT*,'RDLAPSBKG: No SH background available at time frame: ',T
      IF (NUMSTAT .GT. 4) STOP
    ENDIF
    print*,'Minvalue of LAPS SH: ',minval(qq(1:FCSTGRD(1),1:FCSTGRD(2),1:FCSTGRD(3),T))

  ENDDO

  ! INTERPOLATE THE BACKGROUND AT PREVIOUS AND AFTER TIME FRAMES:
  ! INTERPOLATION:	PREVIOUS TIME FRAME
  DO I=1,FCSTGRD(1)
  DO J=1,FCSTGRD(2)
  DO K=1,FCSTGRD(3)
    ZZ(I,J,K,1)=0.5*(ZZ(I,J,K,1)+ZZ(I,J,K,2))
    TT(I,J,K,1)=0.5*(TT(I,J,K,1)+TT(I,J,K,2))
    UU(I,J,K,1)=0.5*(UU(I,J,K,1)+UU(I,J,K,2))
    VV(I,J,K,1)=0.5*(VV(I,J,K,1)+VV(I,J,K,2))
    QQ(I,J,K,1)=0.5*(QQ(I,J,K,1)+QQ(I,J,K,2))	! G/KG -- LAPS USES KG/KG
  ENDDO
  ENDDO
  ENDDO
  ! EXTRAPOLATION: AFTER TIME FRAME
  DO I=1,FCSTGRD(1)
  DO J=1,FCSTGRD(2)
  DO K=1,FCSTGRD(3)
    ZZ(I,J,K,3)=1.5*ZZ(I,J,K,2)-0.5*ZZ(I,J,K,1)
    TT(I,J,K,3)=1.5*TT(I,J,K,2)-0.5*TT(I,J,K,1)
    UU(I,J,K,3)=1.5*UU(I,J,K,2)-0.5*UU(I,J,K,1)
    VV(I,J,K,3)=1.5*VV(I,J,K,2)-0.5*VV(I,J,K,1)
    QQ(I,J,K,3)=amax1(0.0,1.5*QQ(I,J,K,2)-0.5*QQ(I,J,K,1))
  ENDDO
  ENDDO
  ENDDO
  DO I=1,FCSTGRD(1)
  DO J=1,FCSTGRD(2)
  DO K=1,FCSTGRD(3)
    QQ(I,J,K,1:3)=QQ(I,J,K,1:3)*1000.0		! G/KG -- LAPS USES KG/KG
  ENDDO
  ENDDO
  ENDDO
  print*,'Minvalue of interpolated SH: 1',minval(qq(1:FCSTGRD(1),1:FCSTGRD(2),1:FCSTGRD(3),1))
  print*,'Minvalue of interpolated SH: 2',minval(qq(1:FCSTGRD(1),1:FCSTGRD(2),1:FCSTGRD(3),2))
  print*,'Minvalue of interpolated SH: 3',minval(qq(1:FCSTGRD(1),1:FCSTGRD(2),1:FCSTGRD(3),3))

  ! DOMAIN CONFIGURATION:
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'LAT',&
                        LA,ST)
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'LON',&
                        LO,ST)
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'AVG',&
                        AG,ST)
  ! PRESSURE LEVELS:
  CALL GET_PRES_1D(I4,FCSTGRD(3),P1,ST)

  ! GRID SPACING:
  CALL GET_GRID_SPACING_ACTUAL(LA((FCSTGRD(1)-1)/2+1, &
                                  (FCSTGRD(2)-1)/2+1), &
                               LO((FCSTGRD(1)-1)/2+1, &
                                  (FCSTGRD(2)-1)/2+1),DS,ST)
amx = 0.0
do t=1,fcstgrd(4)
do k=1,fcstgrd(3)-1
do j=1,fcstgrd(2)
do i=1,fcstgrd(1)
  ! hy(i,j,k) = 9.8*(zz(i,j,k+1,2)-zz(i,j,k,2))+0.5*287*(tt(i,j,k+1,2)+tt(i,j,k,2))* &
  !          (log(p1(k+1)-log(p1(k))))
  hy(i,j,k) = (p1(k+1)-p1(k))/(zz(i,j,k+1,t)-zz(i,j,k,t))+&
              (p1(k+1)+p1(k))*0.5*9.8/(287*0.5*(tt(i,j,k+1,t)+tt(i,j,k,t)))
  if (amx .lt. hy(i,j,k)*hy(i,j,k)) then
    mxi = i
    mxj = j
    mxk = k
    mxt = t
    amx = hy(i,j,k)*hy(i,j,k)
  endif
enddo
enddo
enddo
enddo
print*,'nonhydrostatic: ',amx,mxi,mxj,mxk
!write(14,*) fcstgrd(1:3),hy(1:fcstgrd(1),1:fcstgrd(2),1:fcstgrd(3))

  DX0=DS(1)
  DY0=DX0 !DS(2)

  ! CONVERT TO REAL 8:
  Y00 = LA
  X00 = LO
  TPG = AG
  P00 = P1

  ! END OF YUANFU'S MODIFICATION

  DO K=1,FCSTGRD(3)
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    S00(I,J,K,1)=1.0D0
  ENDDO
  ENDDO
  ENDDO
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    D00(I,J)=0.0D0
    C00(I,J)=2.0*7.29E-5*SIN(D2R*Y00(I,J))
  ENDDO
  ENDDO
  DO T=1,FCSTGRD(4)
  DO K=1,FCSTGRD(3)
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    BK0(I,J,K,T,U_CMPNNT)=UU(I,J,K,T)
    BK0(I,J,K,T,V_CMPNNT)=VV(I,J,K,T)
    BK0(I,J,K,T,PRESSURE)=ZZ(I,J,K,T)
    BK0(I,J,K,T,TEMPRTUR)=TT(I,J,K,T)
    BK0(I,J,K,T,HUMIDITY)=QQ(I,J,K,T)
    !added by shuyuan for ref  20100811
    BK0(I,J,K,T,ROUR_CMPNNT) =0.0
    BK0(I,J,K,T,ROUS_CMPNNT) =0.0 
    ! ROUR and ROUS  are the input variable. when they are caculated in the COSTFUNT2, they have to be changed to 
    !rc=(17300**(4/7)*ROUR*1000)**(7/4)  rour  kg/m**3
    !rc=(17300**(4/7)*ROUR)**(7/4)  rour  g/m**3
    !sc=(38000**(5/11)*ROUS*1000)**(11/5)so the BK0(,,,,6) and BK0(,,,,7) must be converted.
    !Then the unit of rc and sc and the BK0(,,,,6) and BK0(,,,,7) are db

    !if(BK0(I,J,K,T,ROUR_CMPNNT).NE. 0)then
      !BK0(I,J,K,T,ROUR_CMPNNT) =17300*(BK0(I,J,K,T,ROUR_CMPNNT)*1000) **1.75
    !endif
    !if(BK0(I,J,K,T,ROUS_CMPNNT).NE. 0)then
      !BK0(I,J,K,T,ROUS_CMPNNT) =38000*(BK0(I,J,K,T,ROUS_CMPNNT)*1000) **2.2
    !endif

  ENDDO
  ENDDO
  ENDDO
  ENDDO
  CALL GET_DIRECTORY('static',DR,LN)
!===============
  DO K=1,FCSTGRD(3)
    Z_FCSTGD(K)=P00(K)
  ENDDO
!===============
  OX=X00(1,1)
  EX=X00(FCSTGRD(1),FCSTGRD(2))
  OY=Y00(1,1)
  EY=Y00(FCSTGRD(1),FCSTGRD(2))
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,UU,'UB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,VV,'VB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,TT,'TB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,ZZ,'ZB.GRD')
!  CALL DRCONTOUR_2D(OX,EX,OY,EY,FCSTGRD(1),FCSTGRD(2),TPG,'GG.GRD')

  ORI_LON=OX
  ORI_LAT=OY
  END_LON=EX
  END_LAT=EY

!============= to check the background======
!  OPEN(1,FILE='CHECKBAK.DAT',ACTION='WRITE')
!    DO I=1,FCSTGRD(1)
!    DO J=1,FCSTGRD(2)
!    DO K=6,6    !  1,FCSTGRD(3)
!      WRITE(1,'(3I5,9F10.2)') I,J,K,X00(I,J),Y00(I,J),P00(K),UU(I,J,K),VV(I,J,K),ZZ(I,J,K),TT(I,J,K)
!    ENDDO
!    ENDDO
!    ENDDO
!  CLOSE(1)
!===========================================
  RETURN
END SUBROUTINE RDLAPSBKG


SUBROUTINE RDBKGTEST
!*************************************************
! READ IN TEST DATA OF BACKGROUND
! HISTORY: FEBRUARY 2008, CODED by ZHONGJIE HE.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER  :: I,J,K,T,LN
  CHARACTER(LEN=200) :: DR
  REAL  :: UU(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: VV(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: ZZ(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: TT(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: WW(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: PP(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4))
  REAL  :: P(FCSTGRD(3)),SM(FCSTGRD(3)),Z(FCSTGRD(3))
  REAL  :: OX,OY,EX,EY
  REAL  :: SC          ! COEFFICIENT TO TRANSLATE THE X, Y COORDINATES TO LENGTH WITH UNIT OF METERS.

  REAL :: D2R
  D2R = 3.14159/180.0
!DECLARE:
!        'ZZ' IS THE HEIGHT AT EVERY GRIDPOINT
!        'TT' IS THE TEMPETURE AT EVERY GRIDPOINT
!        'UU' IS THE WEST-EAST VELOCITY AT EACH GRIDPOINT
!        'VV' IS THE SOUTH-NORTH VELOCITY AT EACH GRIDPOINT
!        'WW' IS THE VERTICAL VELOCITY AT EACH GRIDPOINT
!        'PP' IS THE PRESSURE OF EACH LEVEL
! --------------------
  SC=XYTRANS
  IF(IFPCDNT.EQ.0)THEN           ! FOR SIGMA COORDINATE
    OPEN(11,FILE='true.DAT',STATUS='OLD',ACTION='READ')
      DO T=1,FCSTGRD(4)
      DO K=1,FCSTGRD(3)
      DO J=1,FCSTGRD(2)
      DO I=1,FCSTGRD(1)
        READ(11,*) X00(I,J),Y00(I,J),SM(K),T00(T),UU(I,J,K,T),VV(I,J,K,T),WW(I,J,K,T),PP(I,J,K,T),HEIGHTU(I,J),HEIGHTL(I,J)
      ENDDO
      ENDDO
      ENDDO
      ENDDO
    CLOSE(11)
  ELSEIF(IFPCDNT.EQ.1) THEN        ! FOR PRESSURE COORDINATE
    OPEN(11,FILE='true.DAT',STATUS='OLD',ACTION='READ')
      DO T=1,FCSTGRD(4)
      DO K=1,FCSTGRD(3)
      DO J=1,FCSTGRD(2)
      DO I=1,FCSTGRD(1)
        READ(11,*) X00(I,J),Y00(I,J),ZZ(I,J,K,T),T00(T),UU(I,J,K,T),VV(I,J,K,T),WW(I,J,K,T),P(K)
!        READ(11,*) X00(I,J),Y00(I,J),ZZ(I,J,K,T),UU(I,J,K,T),VV(I,J,K,T),WW(I,J,K,T),P(K)  ! ,tt(i,j,k,T)
      ENDDO
      ENDDO
      ENDDO
      ENDDO
    CLOSE(11)
  ELSEIF(IFPCDNT.EQ.2) THEN      ! FOR HEIGHT COORDINATE
    OPEN(11,FILE='true.DAT',STATUS='OLD',ACTION='READ')
      DO T=1,FCSTGRD(4)
      DO K=1,FCSTGRD(3)
      DO J=1,FCSTGRD(2)
      DO I=1,FCSTGRD(1)
        READ(11,*) X00(I,J),Y00(I,J),Z(K),T00(T),UU(I,J,K,T),VV(I,J,K,T),WW(I,J,K,T),PP(I,J,K,T)
      ENDDO
      ENDDO
      ENDDO
      ENDDO
    CLOSE(11)
  ENDIF

  PRINT*, 'X,Y=',X00(1,1),X00(FCSTGRD(1),FCSTGRD(2)),Y00(1,1),Y00(FCSTGRD(1),FCSTGRD(2))

  DO T=1,FCSTGRD(4)
  DO K=1,FCSTGRD(3)
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    S00(I,J,K,T)=1.0D0
  ENDDO
  ENDDO
  ENDDO
  ENDDO
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    D00(I,J)=0.0D0
    C00(I,J)=2.0*7.29E-5*SIN(D2R*30.)
  ENDDO
  ENDDO
  IF(IFPCDNT.EQ.1)THEN       ! FOR PRESSURE COORDINATE
    DO T=1,FCSTGRD(4)
    DO K=1,FCSTGRD(3)
    DO J=1,FCSTGRD(2)
    DO I=1,FCSTGRD(1)
      BK0(I,J,K,T,U_CMPNNT)= UU(I,J,K,T)
      BK0(I,J,K,T,V_CMPNNT)= VV(I,J,K,T)
      BK0(I,J,K,T,PRESSURE)= ZZ(I,J,K,T)
      BK0(I,J,K,T,TEMPRTUR)= TT(I,J,K,T) 
    ENDDO
    ENDDO
    ENDDO
    ENDDO
  ELSE                        !  FOR SIGMA OR HEIGHT COORDINATE
    DO T=1,FCSTGRD(4)
    DO K=1,FCSTGRD(3)
    DO J=1,FCSTGRD(2)
    DO I=1,FCSTGRD(1)
      BK0(I,J,K,T,U_CMPNNT)=0         ! UU(I,J,K,T)
      BK0(I,J,K,T,V_CMPNNT)=0         ! VV(I,J,K,T)
      BK0(I,J,K,T,PRESSURE)=0         ! PP(I,J,K,T)
      BK0(I,J,K,T,TEMPRTUR)=0         ! TT(I,J,K,T)
    ENDDO
    ENDDO
    ENDDO
    ENDDO
  ENDIF
  CALL GET_DIRECTORY('static',DR,LN)
  IF(IFPCDNT.EQ.0)THEN                  !  =========FOR SIGMA COORDINATE =============
    DO K=1,FCSTGRD(3)
      Z00(K)=SM(K)
      Z_FCSTGD(K)= Z00(K)
    ENDDO
  ELSEIF(IFPCDNT.EQ.1) THEN             !  =========FOR P COORDINATE =============
    DO K=1,FCSTGRD(3)
      P00(K)=P(K)
      Z_FCSTGD(K)= P00(K)
    ENDDO
  ELSEIF(IFPCDNT.EQ.2) THEN             !  =========FOR HEIGHT COORDINATE =============
    DO K=1,FCSTGRD(3)
      Z00(K)=Z(K)
      Z_FCSTGD(K)= Z00(K)
    ENDDO
  ENDIF

!  =======================================
  OX=X00(1,1)
  EX=X00(FCSTGRD(1),FCSTGRD(2))
  OY=Y00(1,1)
  EY=Y00(FCSTGRD(1),FCSTGRD(2))
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,UU,'UB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,VV,'VB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,TT,'TB.GRD')
!  CALL DRCONTOUR(OX,EX,OY,EY,NUMDIMS,FCSTGRD,ZZ,'ZB.GRD')
!  CALL DRCONTOUR_2D(OX,EX,OY,EY,FCSTGRD(1),FCSTGRD(2),TPG,'GG.GRD')

  DX0=(X00(FCSTGRD(1),FCSTGRD(2))-X00(1,1))/(FCSTGRD(1)-1)*SC
  DY0=(Y00(FCSTGRD(1),FCSTGRD(2))-Y00(1,1))/(FCSTGRD(2)-1)*SC
  DT0=(T00(FCSTGRD(4))-T00(1))/(FCSTGRD(4)-1)

  IF(IFPCDNT.EQ.0)THEN       !  =========FOR SIGMA COORDINATE =============
    DO K=1,FCSTGRD(3)-1
    DO J=1,FCSTGRD(2)
    DO I=1,FCSTGRD(1)
      DZ0(I,J,K)=(HEIGHTU(I,J)-HEIGHTL(I,J))*(SM(K+1)-SM(K))
    ENDDO
    ENDDO
    ENDDO
  ENDIF
  ORI_LON=OX
  ORI_LAT=OY
  END_LON=EX
  END_LAT=EY

  RETURN

END SUBROUTINE RDBKGTEST

END MODULE READ_BACKGRD
