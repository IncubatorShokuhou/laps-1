MODULE OUTPUT_ANALS

  USE PRMTRS_STMAS
  USE GENERALTOOLS

CONTAINS

SUBROUTINE OUTPTLAPS
!*************************************************
! GET BACKGROUND FOR ANALYSIS
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S
  REAL(KIND=8)    :: XB(MAXGRID(1)),YB(MAXGRID(2)),ZB(MAXGRID(3)),TB(MAXGRID(4))
  REAL(KIND=8)    :: XF(FCSTGRD(1)),YF(FCSTGRD(2)),ZF(FCSTGRD(3)),TF(FCSTGRD(4))
  REAL(KIND=8)    :: BK0(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4),NUMSTAT)
  INTEGER(KIND=4) :: FG(MAXDIMS),MG(MAXDIMS)
  REAL(KIND=8)    :: Z1(1),T1(1),Z2(1),T2(1),DX,DY



  ! VARIABLES NEEDED FOR LAPS: YUANFU
  CHARACTER*9   :: A9
  CHARACTER*3   :: WN(3)=(/'U3','V3','OM'/)	! WIND NAMES
  CHARACTER*3   :: SN(2)=(/'SU','SV'/)		! WIND NAMES
  CHARACTER*4   :: WU(3)=(/'M/S ','M/S ','PA/S'/) ! WIND UNITS
  CHARACTER*125 :: WC(3)=(/'3DWIND','3DWIND','3DWIND'/) ! WIND COMMENTS
  CHARACTER*125 :: SC(2)=(/'SFCWIND','SFCWIND'/) ! SFC WIND COMMENTS
  INTEGER       :: I4,ST
  REAL          :: LA(FCSTGRD(1),FCSTGRD(2)),LO(FCSTGRD(1),FCSTGRD(2))
  REAL          :: TP(FCSTGRD(1),FCSTGRD(2)),DS,LV(FCSTGRD(3))
  REAL          :: HT(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3))
  REAL          :: T3(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3))
  REAL          :: W3(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),2)	! 3D WIND: UV
  REAL          :: SF(FCSTGRD(1),FCSTGRD(2),2)	! SURFACE WIND: UV
  REAL          :: SP(FCSTGRD(1),FCSTGRD(2))	! SURFACE PRESSURE
  REAL          :: HEIGHT_TO_ZCOORD3,RM,A


! --------------------
  DO I=1,FCSTGRD(1)
    XF(I)=(I-1)*1.0D0
  ENDDO
  DO J=1,FCSTGRD(2)
    YF(J)=(J-1)*1.0D0
  ENDDO
  DX=((FCSTGRD(1)-1)*1.0D0)/((MAXGRID(1)-1)*1.0D0)
  DO I=1,MAXGRID(1)
    XB(I)=XF(1)+(I-1)*DX
  ENDDO
  DY=((FCSTGRD(2)-1)*1.0D0)/((MAXGRID(2)-1)*1.0D0)
  DO J=1,MAXGRID(2)
    YB(J)=YF(1)+(J-1)*DY
  ENDDO
  OPEN(2,FILE='VERTICAL.DAT',STATUS='OLD',ACTION='READ')
  DO K=1,FCSTGRD(3)
    READ(2,*)ZF(K)
  ENDDO
  CLOSE(2)
  OPEN(2,FILE='P_LEVEL.DAT',STATUS='OLD',ACTION='READ')
  DO K=1,MAXGRID(3)
    READ(2,*)ZB(K)
  ENDDO
  CLOSE(2)
  DO T=1,FCSTGRD(4)
    TF(T)=0.0D0
  ENDDO
  DO T=1,MAXGRID(4)
    TB(T)=0.0D0
  ENDDO
  CALL FCST2BKGD(NUMDIMS,NGPTOBS,NUMSTAT,MAXGRID,XB,YB,ZB,TB,FCSTGRD,XF,YF,ZF,TF,GRDBKGD0,BK0)

  ! OUTPUT TO LAPS BY YUANFU:
  ! BK0: UVZT: FCSTGRD


  ! OUTPUT TO LAPS BY YUANFU:
  CALL GET_SYSTIME(I4,A9,ST)		! GET SYSTEM TIME
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'LAT',LA,ST)
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'LON',LO,ST)
  CALL READ_STATIC_GRID(FCSTGRD(1),FCSTGRD(2),'AVG',TP,ST)
  CALL GET_GRID_SPACING_ACTUAL(LA((FCSTGRD(1)-1)/2+1,(FCSTGRD(2)-1)/2+1), &
                                LO((FCSTGRD(1)-1)/2+1,(FCSTGRD(2)-1)/2+1),DS,ST)
  ! PRESSURE LEVELS:
  CALL GET_PRES_1D(I4,FCSTGRD(3),LV,ST)
  CALL GET_R_MISSING_DATA(RM,ST)

  ! OUTPUT: WIND BY YUANFU --

  ! INTERPOLATE SURFACE WIND:
  HT = BK0(:,:,:,1,3)
  W3 = BK0(:,:,:,1,1:2)
  DO J=1,FCSTGRD(2)
  DO I=1,FCSTGRD(1)
    SP(I,J) = HEIGHT_TO_ZCOORD3(TP(I,J),HT,LV,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),I,J,ST)
    K = INT(SP(I,J))
    A = SP(I,J)-K
    SF(I,J,1:2) = (1.0-A)*W3(I,J,K,1:2)+A*W3(I,J,K+1,1:2)
  ENDDO
  ENDDO

  ! WRITE SURFACE WIND (THIS IS ALSO REQIRED TO PLOT 3D WIND ON-THE-FLY):
  CALL PUT_LAPS_MULTI_2D(I4,'lwm',SN,WU,SC,SF,FCSTGRD(1),FCSTGRD(2),2,ST)
  CALL WIND_POST_PROCESS(I4,'lw3',WN,WU,WC,W3(1,1,1,1),W3(1,1,1,2), &
                          FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),3,SF(1,1,1),SF(1,1,2),TP, &
                          LA,LO,DS,SP,RM,.TRUE.,ST)

  ! WRITE TEMPERATURE: W3 ARRAY SERVES AS WORKING ARRAY:
  T3(:,:,:) = BK0(:,:,:,1,4)
  CALL WRITE_TEMP_ANAL(I4,FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),T3,HT,W3,A9,ST)

  ! END OF OUTPUT TO LAPS BY YUANFU
  CALL BKGMEMRLS
  RETURN
END SUBROUTINE OUTPTLAPS

SUBROUTINE OUTPUTANA
!*************************************************
! GET BACKGROUND FOR ANALYSIS
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S
  REAL(KIND=8)    :: XB(MAXGRID(1)),YB(MAXGRID(2)),ZB(MAXGRID(3)),TB(MAXGRID(4))
  REAL(KIND=8)    :: XF(FCSTGRD(1)),YF(FCSTGRD(2)),ZF(FCSTGRD(3)),TF(FCSTGRD(4))
  REAL(KIND=8)    :: BK0(FCSTGRD(1),FCSTGRD(2),FCSTGRD(3),FCSTGRD(4),NUMSTAT)
  INTEGER(KIND=4) :: FG(MAXDIMS),MG(MAXDIMS)
  REAL(KIND=8)    :: Z1(1),T1(1),Z2(1),T2(1),DX,DY


! --------------------
  DO I=1,FCSTGRD(1)
    XF(I)=(I-1)*1.0D0
  ENDDO
  DO J=1,FCSTGRD(2)
    YF(J)=(J-1)*1.0D0
  ENDDO
  DX=((FCSTGRD(1)-1)*1.0D0)/((MAXGRID(1)-1)*1.0D0)
  DO I=1,MAXGRID(1)
    XB(I)=XF(1)+(I-1)*DX
  ENDDO
  DY=((FCSTGRD(2)-1)*1.0D0)/((MAXGRID(2)-1)*1.0D0)
  DO J=1,MAXGRID(2)
    YB(J)=YF(1)+(J-1)*DY
  ENDDO
  OPEN(2,FILE='VERTICAL.DAT',STATUS='OLD',ACTION='READ')
  DO K=1,FCSTGRD(3)
    READ(2,*)ZF(K)
  ENDDO
  CLOSE(2)
  OPEN(2,FILE='P_LEVEL.DAT',STATUS='OLD',ACTION='READ')
  DO K=1,MAXGRID(3)
    READ(2,*)ZB(K)
  ENDDO
  CLOSE(2)
  DO T=1,FCSTGRD(4)
    TF(T)=0.0D0
  ENDDO
  DO T=1,MAXGRID(4)
    TB(T)=0.0D0
  ENDDO
  CALL FCST2BKGD(NUMDIMS,NGPTOBS,NUMSTAT,MAXGRID,XB,YB,ZB,TB,FCSTGRD,XF,YF,ZF,TF,GRDBKGD0,BK0)
  CALL BKGMEMRLS
  RETURN
END SUBROUTINE OUTPUTANA

SUBROUTINE TMPOUTPUT
!*************************************************
! OUTPUT ANALYSIS
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: I,J,K,T,S
! --------------------
  OPEN(2,FILE='ANALYSIS.DAT')
  DO S=1,NUMSTAT
    DO T=1,NUMGRID(4)
    DO K=1,NUMGRID(3)
    DO J=1,NUMGRID(2)
    DO I=1,NUMGRID(1)
      WRITE(2,*)GRDBKGD0(I,J,K,T,S)
    ENDDO
    ENDDO
    ENDDO
    ENDDO
  ENDDO
  CLOSE(2)
  CALL DRCONTOUR(3,'Z.GRD')
  CALL DRCONTOUR(1,'U.GRD')
  CALL DRCONTOUR(2,'V.GRD')
  CALL DRCONTOUR(4,'T.GRD')
!  IC=(NUMGRID(1)+1)/2
!  JC=(NUMGRID(2)+1)/2
!  CALL RADIALWND(IC,JC)
  CALL BKGMEMRLS
  RETURN
END SUBROUTINE TMPOUTPUT

SUBROUTINE BKGMEMRLS
!*************************************************
! RELEASE MEMORY FOR BACKGROUND ARRAY
! HISTORY: SEPTEMBER 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
  DEALLOCATE(GRDBKGD0)
  DEALLOCATE(XX0)
  DEALLOCATE(YY0)
  DEALLOCATE(CR0)
  DEALLOCATE(DG0)
  DEALLOCATE(DN0)
  IF(IFPCDNT.EQ.0)THEN
    DEALLOCATE(ZZ0)
  ELSEIF(IFPCDNT.EQ.1)THEN
    DEALLOCATE(PP0)
  ENDIF
  RETURN
END SUBROUTINE BKGMEMRLS

SUBROUTINE DRCONTOUR(S,FN)
!*************************************************
! DRAW THE ANALYSIS (AFFILIATE)
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4)  :: I,J,K,T,S,IU
  REAL(KIND=8)     :: MX,MN
  CHARACTER(LEN=5) :: FN
! --------------------
  K=2 !(NUMGRID(3)+1)/2 !NUMGRID(3)
  T=(NUMGRID(4)+1)/2 !NUMGRID(4) !1
  IU=2
  OPEN(IU,FILE=FN,STATUS='UNKNOWN')
  MX=-1000.0
  MN=1000.0
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(GRDBKGD0(I,J,K,T,S).LT.9000.0)THEN
      IF(GRDBKGD0(I,J,K,T,S).GT.MX)MX=GRDBKGD0(I,J,K,T,S)
      IF(GRDBKGD0(I,J,K,T,S).LT.MN)MN=GRDBKGD0(I,J,K,T,S)
    ENDIF
  ENDDO
  ENDDO
  WRITE(IU,'(A4)')'DSAA'
  WRITE(IU,'(2I4)')NUMGRID(1),NUMGRID(2)
  WRITE(IU,*)ORIPSTN(1),ORIPSTN(1)+(NUMGRID(1)-1)*GRDSPAC(1)
  WRITE(IU,*)ORIPSTN(2),ORIPSTN(2)+(NUMGRID(2)-1)*GRDSPAC(2)
  WRITE(IU,*)MN,MX
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(GRDBKGD0(I,J,K,T,S).GT.9000.0)GRDBKGD0(I,J,K,T,S)=2.E38
  ENDDO
  ENDDO
  DO J=1,NUMGRID(2)
    WRITE(IU,*)(GRDBKGD0(I,J,K,T,S),I=1,NUMGRID(1))
  ENDDO
  CLOSE(IU)
  RETURN
END SUBROUTINE DRCONTOUR

SUBROUTINE RADIALWND(IC,JC)
!*************************************************
! DRAW RADIAL WIND (AFFILIATE)
! HISTORY: AUGUST 2007, CODED by WEI LI.
!*************************************************
  IMPLICIT NONE
! --------------------
  INTEGER(KIND=4) :: IU,IC,JC,I,J,K,T
  REAL(KIND=8)    :: DX,DY,DD,MN,MX
  REAL(KIND=8)    :: UV(NUMGRID(1),NUMGRID(2),NUMGRID(3),NUMGRID(4))
! --------------------
  DO T=1,NUMGRID(4)
  DO K=1,NUMGRID(3)
  DO J=1,NUMGRID(2)
  DO I=1,NUMGRID(1)
    IF(I.EQ.IC.AND.J.EQ.JC)THEN
      UV(I,J,K,T)=2.E38
      CYCLE
    ENDIF
    DX=1.0D0*(I-IC)
    DY=1.0D0*(J-JC)
    DD=DSQRT(DX*DX+DY*DY)
    UV(I,J,K,T)=GRDBKGD0(I,J,K,T,1)*DX/DD+GRDBKGD0(I,J,K,T,2)*DY/DD
  ENDDO
  ENDDO
  ENDDO
  ENDDO
  K=(NUMGRID(3)+1)/2 !NUMGRID(3)
  T=(NUMGRID(4)+1)/2 !NUMGRID(4) !1
  IU=2
  OPEN(IU,FILE='RADIAL.GRD',STATUS='UNKNOWN')
  MX=-1000.0
  MN=1000.0
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(UV(I,J,K,T).LT.9000.0)THEN
      IF(UV(I,J,K,T).GT.MX)MX=UV(I,J,K,T)
      IF(UV(I,J,K,T).LT.MN)MN=UV(I,J,K,T)
    ENDIF
  ENDDO
  ENDDO
  WRITE(IU,'(A4)')'DSAA'
  WRITE(IU,'(2I4)')NUMGRID(1),NUMGRID(2)
  WRITE(IU,*)ORIPSTN(1),ORIPSTN(1)+(NUMGRID(1)-1)*GRDSPAC(1)
  WRITE(IU,*)ORIPSTN(2),ORIPSTN(2)+(NUMGRID(2)-1)*GRDSPAC(2)
  WRITE(IU,*)MN,MX
  DO I=1,NUMGRID(1)
  DO J=1,NUMGRID(2)
    IF(UV(I,J,K,T).GT.9000.0)UV(I,J,K,T)=2.E38
  ENDDO
  ENDDO
  DO J=1,NUMGRID(2)
    WRITE(IU,*)(UV(I,J,K,T),I=1,NUMGRID(1))
  ENDDO
  CLOSE(IU)
  RETURN
END SUBROUTINE RADIALWND

END MODULE OUTPUT_ANALS
